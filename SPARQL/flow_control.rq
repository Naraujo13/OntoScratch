PREFIX ctc: <http://www.semanticweb.org/nicol/ontologies/2020/4/CTConcepts/>
PREFIX scratch: <http://www.semanticweb.org/nicol/ontologies/scratch/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
CONSTRUCT {
	?flow_control ctc:ConceptScore ?score
}
WHERE {
	?evaluation rdf:type ctc:CTEvaluation .
	?evaluation ctc:evaluates ?flow_control .
	?flow_control rdf:type ctc:FlowControl .

	?evaluation ctc:uses ?project .
	?project rdf:type scratch:Project .

	# Rule 1
	OPTIONAL{
    ?project scratch:owns ?block .
    ?project scratch:owns ?level_1_block .
    ?block scratch:Connects ?level_1_block
  }
	# Rule 2
	OPTIONAL{
    ?project scratch:owns ?level_2_block .
    { ?level_2_block rdf:type scratch:ForeverBlock } UNION
    { ?level_2_block rdf:type <http://www.semanticweb.org/nicol/ontologies/scratch/Repeat()Block> }
  }
	# Rule 3
	OPTIONAL{
    ?project scratch:owns ?level_3_block .
    ?level_3_block rdf:type <http://www.semanticweb.org/nicol/ontologies/scratch/RepeatUntil()Block>
  }

	BIND(BOUND(?level_1_block) AS ?has_level_1)
	BIND(BOUND(?level_2_block) AS ?has_level_2)
	BIND(BOUND(?level_3_block) AS ?has_level_3)

	BIND(
		if(
			?has_level_3,
			3,
			if(
				?has_level_2,
				2,
				if(
					?has_level_1,
					1,
					0
				)
			)
		)
	AS ?score)
}
